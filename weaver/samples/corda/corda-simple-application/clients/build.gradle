buildscript {
    ext.clikt_version = "2.7.1"
    ext.coroutines_version = "1.3.7"
    ext.protobuf_version = "3.11.1"
    ext.bcprov_version = "1.53"
    ext.grpc_version = "1.29.0" // CURRENT_GRPC_VERSION
    ext.grpc_kotlin_version = "0.1.4" // CURRENT_GRPC_KOTLIN_VERSION
    ext.arrow_version = "0.10.4"
    ext.tokens_release_group = 'com.r3.corda.lib.tokens'
    ext.tokens_release_version = '1.2'

    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '5.0.0'
    id "java"
    id "com.google.protobuf" version "0.8.12"
    id "idea"
    id "application"
}

sourceSets {
    main {
        resources {
            srcDir rootProject.file("config/dev")
        }
        java.srcDirs += 'src/main/kotlin/'
        kotlin.srcDirs += 'src/main/kotlin/'
    }
}

Properties constants = new Properties()

repositories {
    google()
    jcenter()
    mavenCentral()
    mavenLocal()
    maven { url 'https://software.r3.com/artifactory/corda-lib' }
    flatDir {
        dirs '../../fungible-house-token/source/contracts/build/libs'
        dirs '../../fungible-house-token/source/workflows/build/libs'
    }
    if (file("$projectDir/../github.properties").exists()) {
        file("$projectDir/../github.properties").withInputStream { constants.load(it) }
        maven {
            url constants.url
            credentials {
                username constants.username
                password constants.password
            }
        }
    }
    else {
        flatDir {
            dirs '../../../../core/network/corda-interop-app/interop-contracts/build/libs'
            dirs '../../../../core/network/corda-interop-app/interop-workflows/build/libs'
            dirs '../../../../sdks/corda/build/libs'
            dirs '../../../../common/protos-java-kt/build/libs'
        }
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile 'com.google.code.gson:gson:2.8.6'

    // Corda dependencies.
    compile "$corda_release_group:corda-rpc:$corda_release_version"
    
    compile "io.arrow-kt:arrow-core:$arrow_version"
    compile "io.arrow-kt:arrow-fx:$arrow_version"
    compile "io.arrow-kt:arrow-syntax:$arrow_version"

    // CorDapp dependencies.
    compile project(":contracts-kotlin")
    compile project(":workflows-kotlin")
    compile "com.github.ajalt:clikt:$clikt_version"
    
    implementation(group: 'com.fungiblehousetoken', name: 'contracts', version: "1.0")
    implementation(group: 'com.fungiblehousetoken', name: 'workflows', version: "1.0")
    implementation(group: 'com.weaver.corda.app.interop', name: 'interop-contracts', version: "$weaver_corda_version")
    implementation(group: 'com.weaver.corda.app.interop', name: 'interop-workflows', version: "$weaver_corda_version")
    implementation(group: 'com.weaver.corda.sdk', name: 'weaver-corda-sdk', version: "$weaver_corda_version")
    implementation(group: 'com.weaver', name: 'protos-java-kt', version: "$weaver_protos_version")

    // gRPC and protobuf dependencies
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "io.grpc:grpc-kotlin-stub:$grpc_kotlin_version"
    implementation "com.google.protobuf:protobuf-java:$protobuf_version"
    implementation "com.google.protobuf:protobuf-java-util:$protobuf_version"
    implementation "io.grpc:grpc-netty-shaded:$grpc_version"
    implementation "io.grpc:grpc-protobuf:$grpc_version"
    implementation "io.grpc:grpc-stub:$grpc_version"
    implementation "io.grpc:grpc-okhttp:$grpc_version"
    implementation "org.bouncycastle:bcprov-jdk15on:$bcprov_version"
    implementation group: 'org.json', name: 'json', version: '20190722'

    //// Logger dependencies
    compile "org.slf4j:slf4j-simple:1.6.1"

    // Token SDK dependencies.
    implementation "$tokens_release_group:tokens-contracts:$tokens_release_version"
    implementation "$tokens_release_group:tokens-workflows:$tokens_release_version"

}

mainClassName = "com.cordaSimpleApplication.client.CordaClientKt"
jar {
    manifest {
        attributes(
                'Main-Class': 'com.cordaSimpleApplication.client.CordaClientKt'
        )
    }
}

publishing {
    publications {
        client(MavenPublication) {
            groupId = 'com.cordaSimpleApplication'
            artifactId = 'clients'
            version = '0.4'
            artifact("./clients/build/distributions/clients-0.2.zip")
        }
    }
}
