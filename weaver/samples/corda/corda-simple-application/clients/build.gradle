buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '5.0.0'
    id "java"
    id "idea"
    id "application"
}

sourceSets {
    main {
        resources {
            srcDir rootProject.file("config/dev")
        }
        java.srcDirs += 'src/main/kotlin/'
        kotlin.srcDirs += 'src/main/kotlin/'
    }
}

Properties constants = new Properties()

repositories {
    google()
    jcenter()
    mavenCentral()
    mavenLocal()
    maven { url 'https://download.corda.net/maven/corda-lib' }
    flatDir {
        dirs '../../fungible-house-token/source/contracts/build/libs'
        dirs '../../fungible-house-token/source/workflows/build/libs'
    }
    if (file("$projectDir/../github.properties").exists()) {
        file("$projectDir/../github.properties").withInputStream { constants.load(it) }
        maven {
            url constants.url
            credentials {
                username constants.username
                password constants.password
            }
        }
    }
    else {
        flatDir {
            dirs '../../../../core/network/corda-interop-app/interop-contracts/build/libs'
            dirs '../../../../core/network/corda-interop-app/interop-workflows/build/libs'
            dirs '../../../../sdks/corda/build/libs'
            dirs '../../../../common/protos-java-kt/build/libs'
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'com.google.code.gson:gson:2.8.6'

    // Corda dependencies.
    implementation "$corda_core_release_group:corda-core:$corda_core_release_version"
    implementation "$corda_release_group:corda-node-api:$corda_release_version"
    implementation "$corda_release_group:corda:$corda_release_version"
    implementation "$corda_release_group:corda-rpc:$corda_release_version"
    
    implementation "io.arrow-kt:arrow-core:$arrow_version"
    implementation "io.arrow-kt:arrow-fx:$arrow_version"
    implementation "io.arrow-kt:arrow-syntax:$arrow_version"

    // CorDapp dependencies.
    implementation project(":contracts-kotlin")
    implementation project(":workflows-kotlin")
    implementation "com.github.ajalt.clikt:clikt:$clikt_version"
    
    implementation(group: 'com.fungiblehousetoken', name: 'contracts', version: "1.0")
    implementation(group: 'com.fungiblehousetoken', name: 'workflows', version: "1.0")
    implementation(group: 'org.hyperledger.cacti.weaver.imodule.corda', name: 'interop-contracts', version: "$cacti_version")
    implementation(group: 'org.hyperledger.cacti.weaver.imodule.corda', name: 'interop-workflows', version: "$cacti_version")
    implementation(group: 'org.hyperledger.cacti.weaver.sdk.corda', name: 'weaver-sdk-corda', version: "$cacti_version")
    implementation(group: 'org.hyperledger.cacti.weaver.protos', name: 'protos-java-kt', version: "$cacti_version")

    // gRPC and protobuf dependencies
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "io.grpc:grpc-kotlin-stub:$grpc_kotlin_version"
    implementation "io.grpc:grpc-netty-shaded:$grpc_version"
    implementation "io.grpc:grpc-stub:$grpc_version"
    implementation "io.grpc:grpc-okhttp:$grpc_version"
    implementation "io.grpc:grpc-protobuf:$grpc_version"
    implementation group: 'org.json', name: 'json', version: '20190722'

    //// Logger dependencies
    implementation "org.slf4j:slf4j-simple:1.6.1"

    // Token SDK dependencies.
    implementation "$tokens_release_group:tokens-contracts:$tokens_release_version"
    implementation "$tokens_release_group:tokens-workflows:$tokens_release_version"

}

mainClassName = "com.cordaSimpleApplication.client.CordaClientKt"
jar {
    manifest {
        attributes(
                'Main-Class': 'com.cordaSimpleApplication.client.CordaClientKt'
        )
    }
}

publishing {
    publications {
        client(MavenPublication) {
            groupId = 'com.cordaSimpleApplication'
            artifactId = 'clients'
            version = '0.4'
            artifact("./clients/build/distributions/clients-0.2.zip")
        }
    }
}
