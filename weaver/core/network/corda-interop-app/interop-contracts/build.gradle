plugins {
    id "java"
    id "maven-publish"
    id "idea"
}

apply plugin: 'net.corda.plugins.cordapp'

repositories {
    if (github_url == "") {
        println("Fetching Locally built Weaver packages.")
        flatDir {
            dirs '../../../../common/protos-java-kt/build/libs'
        }
    }
}

cordapp {
    targetPlatformVersion corda_platform_version
    minimumPlatformVersion corda_platform_version
    contract {
        name "Interoperability CorDapp"
        vendor "Corda Open Source"
        licence "Apache License, Version 2.0"
        versionId 1
    }
}

sourceSets {
    main {
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // Corda dependencies.
    cordaProvided "$corda_core_release_group:corda-core:$corda_core_release_version"

    testImplementation "junit:junit:$junit_version"
    testImplementation "$corda_release_group:corda-node-driver:$corda_release_version"
    testImplementation "$corda_core_release_group:corda-test-utils:$corda_core_release_version"
    testImplementation "$corda_core_release_group:corda-core-test-utils:$corda_core_release_version"
    
    // weaver-protos
    implementation "org.hyperledger.cacti.weaver.protos:protos-java-kt:$cacti_version"
}

publishing {
    publications {
        contracts(MavenPublication) {
            groupId = project.properties['group']
            artifactId = 'interop-contracts'
            version = project.properties['version']
            from components.cordapp
        }
    }
    repositories {
        maven {
            url github_url
            credentials {
                username github_username
                password github_password
            }
        }
    }
}

test {
    jvmArgs = rootProject.ext.testJvmArgs
}
