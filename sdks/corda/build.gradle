buildscript {
    ext.kotlin_version = "1.3.61"
    ext.coroutines_version = "1.3.3"
    ext.protobuf_version = "3.11.1"
    ext.bcprov_version = "1.53"
    ext.grpc_version = "1.28.1" // CURRENT_GRPC_VERSION
    ext.grpc_kotlin_version = "0.1.3" // CURRENT_GRPC_KOTLIN_VERSION
    ext.corda_release_group = "net.corda"
    ext.corda_core_release_group = "net.corda"
    ext.corda_version = "4.8"
    ext.corda_core_version = "4.8"
    ext.arrow_version = "0.10.4"
    ext.corda_gradle_plugins_version = "5.0.12"
    ext.log4j_version = "2.16.0"
    ext.slf4j_version = "1.7.25"
    ext.weaver_protos_version = "1.5.6"
    ext.weaver_corda_version = "1.2.12"
    
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "java-library"
    id 'org.jetbrains.dokka' version '0.10.1'
    id "maven-publish"
}

apply plugin: "kotlin"

sourceSets {
    main {
        java.srcDirs += 'src/main/kotlin/'
        kotlin.srcDirs += 'src/main/kotlin/'
    }
}

Properties constants = new Properties()

repositories {
    maven { url 'https://repo1.maven.org/maven2' }
    maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda' }
    if (file("$projectDir/./github.properties").exists()) {
        file("$projectDir/github.properties").withInputStream { constants.load(it) }
        maven {
            url	constants.url
            credentials {
                username constants.username
                password constants.password
            }
        }
    }
    else {
        flatDir {
            dirs '../../core/network/corda-interop-app/interop-contracts/build/libs'
            dirs '../../core/network/corda-interop-app/interop-workflows/build/libs'
            dirs '../../common/protos-java-kt/build/libs'
        }
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile 'com.google.code.gson:gson:2.8.6'

    // Corda dependencies.
    compile "$corda_release_group:corda-rpc:$corda_version"

    implementation(group: 'com.weaver.corda.app.interop', name: 'interop-contracts', version: "$weaver_corda_version")
    implementation(group: 'com.weaver.corda.app.interop', name: 'interop-workflows', version: "$weaver_corda_version")
    implementation(group: 'com.weaver', name: 'protos-java-kt', version: "$weaver_protos_version")

    // gRPC and protobuf dependencies
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "io.grpc:grpc-kotlin-stub:$grpc_kotlin_version"
    implementation "io.grpc:grpc-netty-shaded:$grpc_version"
    implementation "io.grpc:grpc-stub:$grpc_version"
    implementation "io.grpc:grpc-okhttp:$grpc_version"
    implementation "org.bouncycastle:bcprov-jdk15on:$bcprov_version"
    implementation group: 'org.json', name: 'json', version: '20190722'
    
    // Logger dependencies
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:${log4j_version}"
    implementation "org.apache.logging.log4j:log4j-web:${log4j_version}"
    implementation "org.slf4j:jul-to-slf4j:$slf4j_version"
    implementation "org.slf4j:slf4j-simple:$slf4j_version"

}


tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        languageVersion = "1.3"
        apiVersion = "1.3"
        jvmTarget = "1.8"
        javaParameters = true
    }
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/dokka"
}

jar {
    // This makes the JAR's SHA-256 hash repeatable.
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

publishing {
    // Handling if github.properties is not specified
    if (!file("$projectDir/./github.properties").exists()) {
        def styler = 'black red green yellow blue magenta cyan white'
                .split().toList().withIndex(30)
                .collectEntries { key, val -> [(key) : { "\033[${val}m${it}\033[0m" }] }
        println "${styler['red']('Warning')}: Publish won't work. Please use github.properties.template to create github.properties"
    }
    publications {
        sdk(MavenPublication) {
            groupId = project.properties['group']
            artifactId = project.properties['name']
            version = project.properties['version']
            from components.java
        }
    }
    repositories {
        maven {
            url constants.url
            credentials {
                username constants.username
                password constants.password
            }
        }
    }
}

tasks.test {
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
    }
}
